       IDENTIFICATION DIVISION.
       PROGRAM-ID. SQTEST2.

      **********************************************************
      * Program name:    SQTEST2
      * Original author: dastagg
      *
      * Maintenence Log
      * Date       Author        Maintenance Requirement
      * ---------  ------------  -------------------------------
      * 2020-07-29 dastagg       Sample: Get and display data from
      *                          Employee table.
      * 2020-07-29 dastagg       Remove CHECKERR module. Add report.
      *
      **********************************************************
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT EMPRPT
           ASSIGN TO "employee-report.rpt"
           ORGANIZATION IS LINE SEQUENTIAL
      *     ASSIGN TO DA-S-EMPRPT
      *     ORGANIZATION IS SEQUENTIAL
           FILE STATUS IS WS-EmpRpt-Status.


       DATA DIVISION.
       FILE SECTION.
       FD  EMPRPT
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  Print-Line        PIC X(131).

       WORKING-STORAGE SECTION.

           COPY "sqlca.cbl".

           EXEC SQL BEGIN DECLARE SECTION END-EXEC.
       01  MYSAMPLE-Employee.
           12 ME-Emp-Number           PIC X(06).
           12 ME-First-Name           PIC X(12).
           12 ME-Middle-Init          PIC X(01).
           12 ME-Last-Name            PIC X(15).
           12 ME-Work-Dept            PIC X(03).
           12 ME-Phone-Number         PIC X(04).
           12 ME-Hire-Date            PIC X(10).
           12 ME-Job-Title            PIC X(08).
           12 ME-Edu-Level            PIC S9(04) COMP-5.
           12 ME-Gender               PIC X(01).
           12 ME-Birth-Date           PIC x(10).
           12 ME-Salary               PIC S9(9)V99 COMP-3.
           12 ME-Bonus                PIC S9(9)V99 COMP-3.
           12 ME-Commission           PIC S9(9)V99 COMP-3.

       01  DB-Connection-Info.
           12 DB-Alias                PIC X(8) VALUE 'SAMPLE'.
           12 DB-User-ID              PIC X(10).
           12 DB-Password.
              15 DB-Passwd-Length     PIC S9(4) COMP-5  VALUE 0.
              15 DB-Passwd-Name       PIC X(18).
           EXEC SQL END DECLARE SECTION END-EXEC.

       01 WS-SQL-STATUS                PIC S9(9) COMP-5.
          88 SQL-STATUS-OK             VALUE    0.
          88 SQL-STATUS-NOT-FOUND      VALUE  100.
          88 SQL-STATUS-DUP            VALUE -803.

       01 WS-FILE-STATUS.
           12 WS-EmpRpt-Status         PIC X(2) VALUE SPACES.
              88 WS-EmpRpt-EOF                  VALUE '10'.
              88 WS-EmpRpt-Okay                 VALUE '00'.

       01  CURRENT-DATE-AND-TIME.
           12 CDT-Full-Date           PIC 9(8).
           12 CDT-Date-Sep REDEFINES CDT-Full-Date.
              15 CDT-Year             PIC 9(4).
              15 CDT-Month            PIC 9(2).
              15 CDT-Day              PIC 9(2).
           12 CDT-Hour                PIC 9(2). *> 00-23
           12 CDT-Minutes             PIC 9(2). *> 00-59
           12 CDT-Seconds             PIC 9(2). *> 00-59
           12 CDT-Hundredths-Of-Secs  PIC 9(2). *> 00-99
           12 CDT-GMT-Diff-Hours      PIC S9(2)
                                      SIGN LEADING SEPARATE.
           12 CDT-GMT-Diff-Minutes    PIC 9(2). *> 00 or 30

       01  WS-Counters.
           12 WS-Employee-Record-Cnt  PIC 9(4) COMP.

       01  R1-Counters.
           12 R1-Max-Lines         PIC 9(4) COMP VALUE 60.
           12 R1-Line-Count        PIC 9(4) COMP VALUE ZEROES.
           12 R1-Line-Advance      PIC 9(4) COMP VALUE ZEROES.
           12 R1-Page-Count        PIC 9(4) COMP VALUE ZEROES.

       01  R1-Page-Header.
           12 FILLER                   PIC X(006) VALUE "Date: ".
           12 R1-HDR-DATE.
              15 R1-HDR-YY             PIC 9(4).
              15 FILLER                PIC X(1) VALUE "-".
              15 R1-HDR-MM             PIC 9(2).
              15 FILLER                PIC X(1) VALUE "-".
              15 R1-HDR-DD             PIC 9(2).
           12 FILLER                   PIC X(021) VALUE SPACE.
           12 FILLER                   PIC X(034)
                 VALUE "    Sample DB Employee Report     ".
           12 FILLER                   PIC X(049) VALUE SPACE.
           12 FILLER                   PIC X(005) VALUE "Page:".
           12 FILLER                   PIC X(001) VALUE SPACE.
           12 R1-HDR-Page-Count        PIC ZZ9.

       01  R1-Column-Header1.
           12 FILLER   PIC X(003) VALUE "Emp".
           12 FILLER   PIC X(004) VALUE SPACES.
           12 FILLER   PIC X(005) VALUE "First".
           12 FILLER   PIC X(010) VALUE SPACES.
           12 FILLER   PIC X(004) VALUE "Last".
           12 FILLER   PIC X(016) VALUE SPACES.
           12 FILLER   PIC X(003) VALUE "Ph.".
           12 FILLER   PIC X(005) VALUE SPACES.
           12 FILLER   PIC X(004) VALUE "Hire".
           12 FILLER   PIC X(004) VALUE SPACES.
           12 FILLER   PIC X(003) VALUE "Job".
           12 FILLER   PIC X(008) VALUE SPACES.
           12 FILLER   PIC X(002) VALUE "Ed".
           12 FILLER   PIC X(005) VALUE SPACES.
           12 FILLER   PIC X(005) VALUE "Birth".

       01  R1-Column-Header2.
           12 FILLER   PIC X(006) VALUE "Number".
           12 FILLER   PIC X(001) VALUE SPACES.
           12 FILLER   PIC X(004) VALUE "Name".
           12 FILLER   PIC X(011) VALUE SPACES.
           12 FILLER   PIC X(004) VALUE "Name".
           12 FILLER   PIC X(012) VALUE SPACES.
           12 FILLER   PIC X(002) VALUE "WD".
           12 FILLER   PIC X(002) VALUE SPACES.
           12 FILLER   PIC X(004) VALUE "Num.".
           12 FILLER   PIC X(004) VALUE SPACES.
           12 FILLER   PIC X(004) VALUE "Date".
           12 FILLER   PIC X(004) VALUE SPACES.
           12 FILLER   PIC X(005) VALUE "Title".
           12 FILLER   PIC X(006) VALUE SPACES.
           12 FILLER   PIC X(002) VALUE "LV".
           12 FILLER   PIC X(006) VALUE SPACES.
           12 FILLER   PIC X(004) VALUE "Date".
           12 FILLER   PIC X(011) VALUE SPACES.
           12 FILLER   PIC X(006) VALUE "Salary".
           12 FILLER   PIC X(011) VALUE SPACES.
           12 FILLER   PIC X(005) VALUE "Bonus".
           12 FILLER   PIC X(005) VALUE SPACES.
           12 FILLER   PIC X(010) VALUE "Commission".

       01  R1-Column-Header3.
           12 FILLER    PIC X(006) VALUE ALL "=".
           12 FILLER    PIC X(001) VALUE ALL SPACES.
           12 FILLER    PIC X(012) VALUE ALL "=".
           12 FILLER    PIC X(001) VALUE ALL SPACES.
           12 FILLER    PIC X(001) VALUE ALL "=".
           12 FILLER    PIC X(001) VALUE ALL SPACES.
           12 FILLER    PIC X(015) VALUE ALL "=".
           12 FILLER    PIC X(001) VALUE ALL SPACES.
           12 FILLER    PIC X(003) VALUE ALL "=".
           12 FILLER    PIC X(001) VALUE ALL SPACES.
           12 FILLER    PIC X(004) VALUE ALL "=".
           12 FILLER    PIC X(001) VALUE ALL SPACES.
           12 FILLER    PIC X(010) VALUE ALL "=".
           12 FILLER    PIC X(001) VALUE ALL SPACES.
           12 FILLER    PIC X(010) VALUE ALL "=".
           12 FILLER    PIC X(001) VALUE ALL SPACES.
           12 FILLER    PIC X(002) VALUE ALL "=".
           12 FILLER    PIC X(001) VALUE ALL SPACES.
           12 FILLER    PIC X(001) VALUE ALL "=".
           12 FILLER    PIC X(001) VALUE ALL SPACES.
           12 FILLER    PIC X(010) VALUE ALL "=".
           12 FILLER    PIC X(001) VALUE ALL SPACES.
           12 FILLER    PIC X(014) VALUE ALL "=".
           12 FILLER    PIC X(001) VALUE ALL SPACES.
           12 FILLER    PIC X(014) VALUE ALL "=".
           12 FILLER    PIC X(001) VALUE ALL SPACES.
           12 FILLER    PIC X(014) VALUE ALL "=".

       01  R1-Detail-Line.
           12 R1-Emp-Number           PIC X(06).
           12 FILLER                  PIC X(001) VALUE SPACES.
           12 R1-First-Name           PIC X(12).
           12 FILLER                  PIC X(001) VALUE SPACES.
           12 R1-Middle-Init          PIC X(01).
           12 FILLER                  PIC X(001) VALUE SPACES.
           12 R1-Last-Name            PIC X(15).
           12 FILLER                  PIC X(001) VALUE SPACES.
           12 R1-Work-Dept            PIC X(03).
           12 FILLER                  PIC X(001) VALUE SPACES.
           12 R1-Phone-Number         PIC X(04).
           12 FILLER                  PIC X(001) VALUE SPACES.
           12 R1-Hire-Date            PIC X(10).
           12 FILLER                  PIC X(001) VALUE SPACES.
           12 R1-Job-Title            PIC X(08).
           12 FILLER                  PIC X(001) VALUE SPACES.
           12 R1-Edu-Level            PIC ZZZ9.
           12 FILLER                  PIC X(001) VALUE SPACES.
           12 R1-Gender               PIC X(01).
           12 FILLER                  PIC X(001) VALUE SPACES.
           12 R1-Birth-Date           PIC X(10).
           12 FILLER                  PIC X(001) VALUE SPACES.
           12 R1-Salary               PIC $$$,$$$,$$9.99.
           12 FILLER                  PIC X(001) VALUE SPACES.
           12 R1-Bonus                PIC $$$,$$$,$$9.99.
           12 FILLER                  PIC X(001) VALUE SPACES.
           12 R1-Commission           PIC $$$,$$$,$$9.99.

       01  R1-Footer1.
           12 FILLER             PIC X(036)
              VALUE "Number of Employees in this Report: ".
           12 FILLER             PIC X VALUE SPACE.
           12 R1-Total-Recs-Read PIC ZZ9.

       01 EOJ-Display-Messages.
           12 EOJ-End-Message PIC X(042) VALUE
              "*** Program SQTEST2 - End of Run Messages".
           12 EOJ-Print-Message PIC X(40) VALUE SPACES.
           12 EOJ-Print-Number  PIC ZZ,ZZ9 VALUE ZEROES.
           12 EOJ-Print-Money   PIC $$,$$9.99 VALUE ZEROES.


       PROCEDURE DIVISION.
       0000-Mainline.
           PERFORM 1000-BOJ.
           PERFORM 2000-Process.
           PERFORM 3000-EOJ.
           GOBACK.

       1000-BOJ.
           PERFORM 1010-Get-Credentials.
           PERFORM 1800-Setup-DB1-Connection.
           IF SQL-STATUS-OK
              OPEN OUTPUT EMPRPT
              PERFORM 6101-Setup-R1
              PERFORM 6110-Write-R1-Page-Header
              PERFORM 5000-Read-DB1
           END-IF.

       1010-Get-Credentials.
           DISPLAY "SQTEST1: Display MYSAMPLE: Employee Info.".

           DISPLAY "Enter your user id (default none): "
                WITH NO ADVANCING.
           ACCEPT DB-User-ID.

           IF DB-User-ID = SPACES
             EXEC SQL CONNECT TO mysample END-EXEC
           ELSE
             DISPLAY "Enter your password : " WITH NO ADVANCING
             ACCEPT DB-Passwd-Name.

      * Passwords in a CONNECT statement must be entered in a VARCHAR
      * format with the length of the input string.
           INSPECT DB-Passwd-Name
              TALLYING DB-Passwd-Length
              FOR CHARACTERS BEFORE INITIAL " ".

       1800-Setup-DB1-Connection.
           PERFORM 1810-Connect-To-DB1
           IF SQL-STATUS-OK
              PERFORM 1820-Create-Cursor-DB1
              IF SQL-STATUS-OK
                 PERFORM 1830-Open-Cursor-DB1
              END-IF
           END-IF.

       1810-Connect-To-DB1.
           EXEC SQL CONNECT TO :DB-Alias
                USER :DB-User-ID
                USING :DB-Passwd-Name
              END-EXEC.
           MOVE SQLCODE TO WS-SQL-STATUS.

       1820-Create-Cursor-DB1.
           EXEC SQL DECLARE c1 CURSOR FOR
                    SELECT EMPNO,
                           FIRSTNME,
                           MIDINIT,
                           LASTNAME,
                           WORKDEPT,
                           PHONENO,
                           HIREDATE,
                           JOB,
                           EDLEVEL,
                           SEX,
                           BIRTHDATE,
                           SALARY,
                           BONUS,
                           COMM
                    FROM EMPLOYEE
           END-EXEC.

       1830-Open-Cursor-DB1.
           EXEC SQL OPEN c1 END-EXEC.
           MOVE SQLCODE TO WS-SQL-STATUS.

       2000-Process.
           PERFORM 2100-Process-Data
              UNTIL NOT SQL-STATUS-OK.

       2100-Process-Data.
           IF SQL-STATUS-OK
              PERFORM 2110-Move-Detail-Data
              MOVE 1 TO R1-Line-Advance
              PERFORM 6100-Write-R1
              PERFORM 5000-Read-DB1
           END-IF.

       2110-Move-Detail-Data.
           MOVE ME-Emp-Number TO R1-Emp-Number.
           MOVE ME-First-Name TO R1-First-Name.
           MOVE ME-Middle-Init TO R1-Middle-Init.
           MOVE ME-Last-Name TO R1-Last-Name.
           MOVE ME-Work-Dept TO R1-Work-Dept.
           MOVE ME-Phone-Number TO R1-Phone-Number.
           MOVE ME-Hire-Date TO R1-Hire-Date.
           MOVE ME-Job-Title TO R1-Job-Title.
           MOVE ME-Edu-Level TO R1-Edu-Level.
           MOVE ME-Gender TO R1-Gender.
           MOVE ME-Birth-Date TO R1-Birth-Date.
           MOVE ME-Salary TO R1-Salary.
           MOVE ME-Bonus TO R1-Bonus.
           MOVE ME-Commission TO R1-Commission.

      *     DISPLAY R1-Detail-Line.

       3000-EOJ.
           EXEC SQL CLOSE c1 END-EXEC.
           MOVE SQLCODE TO WS-SQL-STATUS.

           EXEC SQL CONNECT RESET END-EXEC.
           MOVE SQLCODE TO WS-SQL-STATUS.
           MOVE WS-Employee-Record-Cnt TO R1-Total-Recs-Read.
           PERFORM 6130-Write-R1-Footer.
           CLOSE EMPRPT.

       5000-Read-DB1.
           EXEC SQL FETCH c1
              INTO
                 :ME-Emp-Number,
                 :ME-First-Name,
                 :ME-Middle-Init,
                 :ME-Last-Name,
                 :ME-Work-Dept,
                 :ME-Phone-Number,
                 :ME-Hire-Date,
                 :ME-Job-Title,
                 :ME-Edu-Level,
                 :ME-Gender,
                 :ME-Birth-Date,
                 :ME-Salary,
                 :ME-Bonus,
                 :ME-Commission
           END-EXEC.
           MOVE SQLCODE TO WS-SQL-STATUS.
           IF SQL-STATUS-OK
              ADD +1 TO WS-Employee-Record-Cnt
           ELSE
             IF SQL-STATUS-NOT-FOUND
                NEXT SENTENCE
             ELSE
              DISPLAY "*** WARNING ***"
              DISPLAY "There was a problem Fetching the cursor."
              DISPLAY "SQLCODE = " SQLCODE
              END-IF
           END-IF.

       6100-Write-R1.
           IF R1-Line-Count + R1-Line-Advance > R1-Max-Lines
              PERFORM 6110-Write-R1-Page-Header
              PERFORM 6120-Write-R1-Detail
           ELSE
              PERFORM 6120-Write-R1-Detail
           END-IF.

       6101-Setup-R1.
           MOVE FUNCTION CURRENT-DATE TO CURRENT-DATE-AND-TIME.
           MOVE CDT-Year     TO R1-HDR-YY.
           MOVE CDT-Month    TO R1-HDR-MM.
           MOVE CDT-Day      TO R1-HDR-DD.

       6110-Write-R1-Page-Header.
           ADD +1 TO R1-Page-Count.
           MOVE R1-Page-Count TO R1-HDR-Page-Count.
           WRITE Print-Line FROM R1-Page-Header
              AFTER ADVANCING PAGE.
           WRITE Print-Line FROM R1-Column-Header1
              AFTER ADVANCING 2.
           WRITE Print-Line FROM R1-Column-Header2
              AFTER ADVANCING 1.
           WRITE Print-Line FROM R1-Column-Header3
              AFTER ADVANCING 1.
           MOVE 5 TO R1-Line-Count.

       6120-Write-R1-Detail.
           MOVE R1-Detail-Line TO Print-Line.
           WRITE Print-Line
              AFTER ADVANCING R1-Line-Advance LINES.
           ADD R1-Line-Advance TO R1-Line-Count.
      *     ADD +1 TO R1-Lines-Written.

       6130-Write-R1-Footer.
           IF R1-Line-Count + 3 > R1-Max-Lines
              PERFORM 6110-Write-R1-Page-Header
           END-IF.
           MOVE R1-Footer1 TO Print-Line.
           WRITE Print-Line
              AFTER ADVANCING 2 LINES.
           PERFORM 6140-Display-EOJ-Messages.

       6140-Display-EOJ-Messages.
           DISPLAY EOJ-End-Message.
